// This is not compliant with https://github.com/hashicorp/hcl/blob/main/hclsyntax/spec.md, but its good enough for many common files.

// The top-level scope of an HCL file
hcl = { SOI ~ (nested_object | assignment)* ~ EOI }

// Key-value pairs, possibly nested, AKA "object items"
nested_object = { ident ~ key* ~ object }
assignment = { key ~ "=" ~ value ~ ","? }

// Object keys
keys = { key{1,} }
key = _{ ident | string }

// Identifiers
ident = @{ ident_first_char ~ ident_char* }
ident_char = _{ ASCII_ALPHA  | ASCII_DIGIT | "_" | "-" | "." }
ident_first_char = _{ ASCII_ALPHA  | "_" } 

// Values
value = _{ list | object | string | number | boolean | heredoc }

// Lists
list = {
    "["
    ~ (value ~ ",")*
    ~ value?
    ~ "]"
}

// Object literals
object = { "{" ~ (nested_object | assignment)* ~ "}" }

// Strings
string = @{ 
    quote 
    ~ (quoted_quote | (!quote ~ ANY))* 
    ~ quote
}
quoted_quote = _{ "\\\"" }
quote = _{ "\""}

// Heredoc
heredoc = ${
    "<<" ~ "-"? ~ WHITESPACE* ~ PUSH(ident) ~ newline
    ~ (!PEEK ~ ANY)*
    ~ WHITESPACE* ~ POP
}

// Numbers
number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

// Booleans
boolean = { "true" | "false" }

// Comments
COMMENT = { inline_comment | block_comment }
inline_comment = { ("#" | "//") ~ (!eo_comment ~ ANY)* ~ eo_comment}
block_comment = { "/*" ~ (!"*/" ~ ANY)* ~ "*/" ~ eo_comment}
eo_comment = { newline | EOI }

// Whitespace
WHITESPACE = _{ " " | "\t" | newline }
newline = _{ "\r\n" | "\n" }
